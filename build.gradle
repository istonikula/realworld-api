buildscript {
  ext {
    kotlinVersion = '1.1.51'
    restAssuredVersion = '3.0.3'
    springBootVersion = '2.0.0.M4'
  }
  repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
    classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
  }
}

configure(subprojects - project(':app')) { project ->
  apply plugin: 'kotlin'
  apply plugin: 'kotlin-spring'
  apply plugin: 'io.spring.dependency-management'

  version = '0.0.1-SNAPSHOT'

  sourceCompatibility = 1.8
  compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
  }
  compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
  }

  repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
  }

  dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlinVersion}")
    compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")

    testCompile('org.springframework.boot:spring-boot-starter-test') {
      exclude group: 'junit', module: 'junit'
    }
    testCompile('io.projectreactor:reactor-test')
    testCompile('org.junit.jupiter:junit-jupiter-api')
    testCompile('org.junit.jupiter:junit-jupiter-engine')
  }
}

project('app:web') {
  apply plugin: 'org.springframework.boot'

  dependencies {
    compile('org.flywaydb:flyway-core')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-webflux')

    runtime('com.h2database:h2')

    testCompile("io.rest-assured:rest-assured:${restAssuredVersion}")
    testCompile('org.springframework.security:spring-security-test')
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.2'
}
